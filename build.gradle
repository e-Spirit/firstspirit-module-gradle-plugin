/*
 * Copyright 2013 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
	}
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7
targetCompatibility = 1.7

description = 'Gradle plugin to build FirstSpirit modules (FSMs).'
group = 'com.espirit.moddev'
version = '0.4.0_DEV'
ext.pluginId = 'com.espirit.moddev.fsm-gradle-plugin'
ext.artifactName = 'fsm-gradle-plugin'

repositories {
	mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.reflections:reflections:0.9.11'
	
    testCompile (
		'junit:junit:4.12',
		'org.hamcrest:hamcrest-all:1.3'
	)
}

plugins.withType(JavaPlugin) {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

jar {
	manifest {
		attributes 'Implementation-Title': 'Gradle FSM plugin',
				   'Implementation-Version': version,
				   'Built-By': System.getProperty('user.name'),
				   'Built-Date': new Date(),
				   'Built-JDK': System.getProperty('java.version'),
				   'Built-Gradle': gradle.gradleVersion
	}
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId artifactName
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'FSM Gradle plugin')
                root.appendNode('description', 'Gradle plugin to build FirstSpirit modules (FSMs).')
                root.appendNode('inceptionYear', '2013')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developers = root.appendNode('developers')
                def developer = developers.appendNode('developer')
                developer.appendNode('id', 'moritzzimmer')
                developer.appendNode('name', 'Moritz Zimmer')
                developer.appendNode('email', 'moritz.zmmr@gmail.com')
            }
        }
    }
}

task install(dependsOn: 'publishMavenJavaPublicationToMavenLocal'
        , group:'publishing', description: 'Installs the maven Java artifact to the local Maven repository') << {
    logger.info "Installed $project.name"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}
